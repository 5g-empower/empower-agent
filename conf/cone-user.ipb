# Run this on cone.
# I use it for a benchmark.

# darkstar 18.26.4.60  00:80:C8:4B:25:00
# tsb      18.26.4.200 00:a0:c9:9c:fd:9c
# zeus     18.26.4.97  00:00:c0:ca:68:ef
# cone     18.26.4.24  121a0418 00:00:C0:AE:67:EF
# panam    18.26.4.38  121a0426

# 0. IP packets to cone.
# 1. IP packets to net 18.26.4.
# 2. ARP responses.
# 3. Others.
fi :: Classifier(12/0800 30/121a0418,
             12/0800 30/121a04 0/0000c0ae67ef,
             12/0806 20/0002,
             -);

FromBPF(eth0) -> [0]fi;
outq :: ToBPF(eth0);

# Common code to send IP packets, including ARP.
# IP dst annotation must already be set.
arpq :: ARPQuerier(18.26.4.24, 00:00:C0:AE:67:EF);
fi[2] -> [1]arpq;
arpq[0] -> outq;

# Route packets between tsb and zeus.
# They attach darkstar's ethernet address to packets,
# so the real work is in ARP, not IP routing.
fi[1] -> Strip(14)
      -> CheckIPHeader
      -> DecIPTTL
#     -> SetIPChecksum
      -> GetIPAddress(16)
      -> [0]arpq;

fi[0] -> Discard;
fi[3] -> Discard;

